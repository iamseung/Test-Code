plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'sample'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // h2
    runtimeOnly 'com.h2database:h2'

    // Guava
    implementation("com.google.guava:guava:33.5.0-jre")

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test' // JUnit + AssertJ 포함
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // restdocs-asciidoctor 의존성 추가
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    // MockMvc 테스트용 의존성
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

}

ext { // 전역변수
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'

    sources {
        // 특정 파일만 html로 만든다.
        include("**/index.adoc")
    }
    // 다른 adoc 파일을 include 할 때 경로를 baseDir로 맞춘다.
    baseDirFollowsSourceFile()
    dependsOn test
}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}") {
        into 'static/docs'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
